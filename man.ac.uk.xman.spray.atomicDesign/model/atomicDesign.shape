/*************************************************************************************
 *
 * Spray shapes definition
 * 
 * This file contains the definition of graphical figures using the Shapes Language.
 * Refer to http://code.google.com/a/eclipselabs.org/p/spray/ for documentation.
 *************************************************************************************/
shape DataItemShape {
	stretching (horizontal=false, vertical=false)
	ellipse {
		style (line-width=2)
		size (width=62, height=62)
		position (x=0, y=0)
		ellipse {
			style (line-color=white)
			position (x=8, y=5)
			size (width=50, height=50)
		}
		text style H2 {
			position (x=15, y=6)
			size (width=10, height=5)
			align (horizontal=center, vertical=middle)
			id = dataItemLabel
		}
	}
}

shape ComputationUnitShape {
	proportional = true
	ellipse {
		compartment(
		  spacing=3
		  layout=fit
		  id = methodsReferences
		  stretching(horizontal=false,vertical=false)	
		)
		style (line-width=2)
		size (width=300, height=300)
		ellipse style BlackBackground {
			position (x=100, y=100)
			size (width=100, height=100)
			text style H2 {
				style (background-color=black font-color=white)
				position (x=25, y=25)
				size (width=25, height=30)
				id = cuLable
			}
		}
	}
}

shape ServiceShape {
	proportional=true
	stretching (horizontal=false, vertical=true)
	rectangle {
		compartment (
			layout = fixed
			id = serviceParameter
		)
		style (line-width=3)
		size (width=250, height=100)
		text {
			style (font-size=14)
			position (x=5, y=2)
			size (width=230, height=10)
			align (horizontal=left, vertical=top)
			id = serviceNameLabel
		}
		line {
			point (x=1, y=25)
			point (x=249, y=25)
		}
	}
}

shape InputShape {
	stretching (horizontal=false, vertical=false)
	rectangle style BlackBackground {
		size (width=20, height=20)
		text {
			style (font-color=white font-bold=yes font-size=15)
			align (horizontal=center, vertical=middle)
			size (width=6, height=8)
			position (x=3, y=-6)
			id = inputParameterLabel
		}
	}
}

shape OutputShape {
	stretching (horizontal=false, vertical=false)
	rectangle style BlackBackground {
		size (width=20, height=20)
		text {
			style (font-color=white font-bold=yes font-size=15)
			align (horizontal=center, vertical=middle)
			size (width=6, height=8)
			position (x=-1, y=-9)
			id = outputParameterLabel
		}
	}
}

shape MethodShape {
	ellipse {
		style (line-width=2)
		size (width=42, height=42)
		polygon style BlackBackground {
			point (x=16, y=11)
			point (x=16, y=31)
			point (x=26, y=21)
		}
	}
	text style H3{
		align(horizontal=left,vertical=top)
		size(width=42,height=20)
		position(x=0, y=43)
		id=methodNameLabel
	}
}

connection AssociationConnection {
	connection-type freeform
	placing {
		position (offset=0.5, radius=10, angle=270)
		text {
			size (width=60, height=20)
			id = assocText
		}
	}
}

connection CompositionConnection {
	connection-type freeform
	placing {
		position (offset=1.0, radius=0, angle=0)
		polygon style BlackBackground {
			point (x=0, y=0)
			point (x=-15, y=-10)
			point (x=-30, y=0)
			point (x=-15, y=10)
		}
	}
}

 